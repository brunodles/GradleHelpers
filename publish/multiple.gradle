apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group mavenRoot.group
version mavenRoot.version

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.artifactId = mavenModule.artifact
        pom.project {
            name mavenModule.name
            url mavenRoot.site
            scm {
                connection mavenRoot.git
                developerConnection mavenRoot.git
                url mavenRoot.git
            }
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId mavenRoot.group
            artifactId mavenModule.artifact
            version mavenRoot.version
        }
    }
}

def property(String projectName, String systemName, def defaultValue) {
    // Load the properties from `~/.gradle/gradle.properties`
    if (project.hasProperty(projectName))
        return project.property(projectName)
    else if (System.getenv().containsKey(systemName))
        return System.getenv(systemName)
    else
        return defaultValue
}

bintray {
    user = property("bintray.user", "BINTRAY_USER", null)
    key = property("bintray.apikey", "BINTRAY_APIKEY", null)
    def bintrayRepo = property("bintray.repo", "BINTRAY_REPO", maven.repo)
    logger.info("Bintray user $user")
    logger.info("Bintray repo $bintrayRepo")
    if (user == null || key == null) {
        logger.info('Bintray user/apikey not found')
        throw new GradleException('Bintray user/apikey not found')
    }
    publications = ['MyPublication']

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = mavenModule.artifact
//        userOrg = mavenRoot.name
        websiteUrl = mavenRoot.site
        vcsUrl = mavenRoot.git
        licenses = ["mit"]
        publish = true
        version {
            name = mavenRoot.version
            released = new Date()
            vcsTag = mavenRoot.tag
        }
    }
}

project.tasks.getByName('bintrayUpload').dependsOn('install')